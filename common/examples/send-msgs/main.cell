schema Counter {
  value:   Int = 0;
  updates: Int = 0;
}

type CounterMsg = incr, decr, reset, reset(Int);

Counter.CounterMsg {
  set value = new_value(this);
  set updates = updates + 1;
}

using Counter {
  Int new_value(CounterMsg msg) =
    incr      = value + 1,
    decr      = value - 1,
    reset     = 0,
    reset(n?) = n;
}

////////////////////////////////////////////////////////////////////////////////

Int Main(String* args) {
  instance : Counter;

  // Checking the argument list
  if |args| != 3 {
    Print("Usage: send-msgs <initial state> <message list> <final state>\n");
    return 1;
  }
  init_state_fname, msg_list_fname, final_state_fname = args;

  // Reading and checking the initial state
  res = ReadValueFromFile(init_state_fname);
  return 1 if res == nothing;
  init_state = value(res);
  if not init_state :: [Symbol -> Any] {
    Print("Invalid initial state\n");
    return 1;
  }

  // Setting the initial state of the automaton
  ok = write instance <- init_state;
  if not ok {
    Print("Invalid initial state\n");
    return 1;
  }

  // Reading and checking the message list
  res = ReadValueFromFile(msg_list_fname);
  return 1 if res == nothing;
  msg_list = value(res);
  if not msg_list :: CounterMsg* {
    Print("Invalid message list\n");
    return 1;
  }

  // Sending all messages in the list
  for msg @ i <- msg_list {
    ok = instance <- msg;
    if not ok
      Print("Message number " & _print_(i) & " failed\n");
  }

  // Saving the final state
  final_state = read instance;
  ok = FileWrite(final_state_fname, (_untag_(_print_(final_state)) | `\n`));
  if not ok {
    Print("Could not write to file " & final_state_fname & "\n");
    return 1;
  }

  return 0;
}

Maybe[Any] ReadValueFromFile(String fname) {
  read_res = FileRead(fname);
  if read_res == nothing {
    Print("Cannot read file " & fname & "\n");
    return nothing;
  }

  parse_res = _parse_(string(value(read_res)));
  if failed(parse_res) {
    Print("File " & fname & " does not contain a valid Cell value\n");
    print string(value(read_res));
    print parse_res;
    return nothing;
  }

  return just(result(parse_res));
}
